apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"


    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def keyAliasStr = properties.getProperty("keyAlias","")
    def keyPasswordStr = properties.getProperty("keyPassword","")
    def fileUrl = properties.getProperty("storeFile","")

    signingConfigs {
        myConfig {
            keyAlias keyAliasStr
            keyPassword keyPasswordStr
            storeFile file(fileUrl)
            storePassword keyPasswordStr
        }
    }

    defaultConfig {
        applicationId "com.ttm.tlrb"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 3
        versionName "1.3.0"
        flavorDimensions "versionCode"
        //multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a','arm64-v8a'
        }
    }
    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.myConfig
        }
    }

    productFlavors {
        /**测试环境*/
        dev{
            buildConfigField "int","SERVER_TYPE","0"
            buildConfigField "String","X_Bmob_Application_Id","\"c1aa552ec6d7639a92f11a362ff22b34\""
            buildConfigField "String","X_Bmob_REST_API_Key","\"a93c8e5501c9f2ed942ae1cff46ccd77\""
            buildConfigField "String","BASE_DOWNLOAD_FILE_URL","\"http://bmob-cdn-2106.b0.upaiyun.com/\""
            manifestPlaceholders = [
                    UMENG_CHANNEL_VALUE: "dev",
                    UMENG_APPKEY_VALUE: "573ecc24e0f55aec6b000976",
                    qqappid:"100424468"
            ]
        }
        /**正式环境*/
        pro{
            buildConfigField "int","SERVER_TYPE","1"
            buildConfigField "String","X_Bmob_Application_Id","\"7886715d834d24b15f1b0947d51b0c6e\""
            buildConfigField "String","X_Bmob_REST_API_Key","\"1487aca64c5144719d49ed9408bc6fa8\""
            buildConfigField "String","BASE_DOWNLOAD_FILE_URL","\"http://bmob-cdn-3839.b0.upaiyun.com/\""
            manifestPlaceholders = [
                    UMENG_CHANNEL_VALUE: "product",
                    UMENG_APPKEY_VALUE: "576b3c8c67e58eaff4001e99",
                    qqappid:"100424468"
            ]
        }

        /*baidu {
            buildConfigField "int","SERVER_TYPE","1"
            buildConfigField "String","X_Bmob_Application_Id","\"7886715d834d24b15f1b0947d51b0c6e\""
            buildConfigField "String","X_Bmob_REST_API_Key","\"1487aca64c5144719d49ed9408bc6fa8\""
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "baidu",UMENG_APPKEY_VALUE: "576b3c8c67e58eaff4001e99"]
        }*/
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'io.reactivex:rxjava:1.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.0.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.alipay.euler:andfix:0.4.0@aar'
    //友盟统计
    implementation  'com.umeng.umsdk:common:9.4.4'// 必选
    implementation  'com.umeng.umsdk:asms:1.4.1'// 必选
    implementation 'com.umeng.umsdk:apm:1.5.2' // 错误分析升级为独立SDK，看crash数据请一定集成，可选
    //友盟第三方登录
    implementation  'com.umeng.umsdk:share-core:7.1.7'//分享核心库，必选
    implementation  'com.umeng.umsdk:share-wx:7.1.7'//微信完整版
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.8.0'//微信官方依赖库，必选
    implementation  'com.umeng.umsdk:share-qq:7.1.7'//QQ完整版
    //implementation files('libs/open_sdk_3.5.7.4_r1bc9afe_lite.jar') //QQ官方依赖库，必选
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'//QQ官方sdk 3.53及之后版本需要集成okhttp3.x，必选
    implementation  'com.umeng.umsdk:share-sina:7.1.7'//新浪微博完整版
    implementation 'io.github.sinaweibosdk:core:11.11.1@aar'//新浪微博官方SDK依赖库，必选

    //Bmob
    //implementation 'cn.bmob.android:bmob-sdk:3.4.7-aar'
    //控件
    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'com.yancy.imageselector:imageselector:1.3.3'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.facebook.fresco:fresco:0.11.0'
}
